#!/usr/bin/env python

Import("env")
Import("editor_env")
import os
import sys
del sys.modules['editor']
from editor.icons import editor_icons_builders

env_icons = env.Clone()

env["BUILDERS"]["MakeEditorIconsBuilder"] = Builder(
    action=Action(editor_icons_builders.make_editor_icons_action, "Generating editor icons header."),
    suffix=".h",
    src_suffix=".svg",
)

# Editor's own icons
icon_sources = env_icons.Glob("*.svg")

# Module icons
for path in env.module_icons_paths:
    if not os.path.isabs(path):
        icon_sources += env_icons.Glob("#" + path + "/*.svg")  # Built-in.
    else:
        icon_sources += env_icons.Glob(path + "/*.svg")  # Custom.

env.Alias("editor_icons", [env.MakeEditorIconsBuilder("editor_icons.gen.h", icon_sources)])
editor_env.Append(CPPPATH=[env.Dir(".")])
