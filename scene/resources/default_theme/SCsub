#!/usr/bin/env python

Import("env")

import os
import os.path

from scene.resources.default_theme import default_theme_builders
from scene.resources.default_theme import default_theme_icons_builders

default_theme_env = env.Clone()

default_theme_env.add_source_files(env.scene_sources, "*.cpp")

default_theme_env.Command(
    "default_font.gen.h",
    "#thirdparty/fonts/OpenSans_SemiBold.ttf",
    default_theme_builders.make_fonts_header,
)

env["BUILDERS"]["MakeDefaultThemeIconsBuilder"] = Builder(
    action=Action(default_theme_icons_builders.make_default_theme_icons_action, "Generating default project theme icons header."
    ),
    suffix=".h",
    src_suffix=".svg",
)

# Default theme icons
icon_sources = Glob("*.svg")

env.Alias(
    "default_theme_icons",
    [env.MakeDefaultThemeIconsBuilder("default_theme_icons.gen.h", icon_sources)],
)
default_theme_env.Append(CPPPATH=[default_theme_env.Dir(".")])
