#!/usr/bin/env python

Import("env")

from main import main_builders

env.main_sources = []

env_main = env.Clone()

env_main.add_source_files(env.main_sources, "*.cpp")

if env["tests"]:
    env_main.Append(CPPDEFINES=["TESTS_ENABLED"])

env_main.Command(
    "splash.gen.h",
    "splash.png",
    Action(main_builders.make_splash, "Building splash screen header."),
)

if not env_main["no_editor_splash"]:
    env_main.Command(
        "splash_editor.gen.h",
        "splash_editor.png",
        Action(main_builders.make_splash_editor, "Building editor splash screen header."),
    )

env_main.Command(
    "app_icon.gen.h",
    "app_icon.png",
    Action(main_builders.make_app_icon, "Building application icon."),
)

lib = env_main.add_library("main", env.main_sources)
env.Prepend(LIBS=[lib])
