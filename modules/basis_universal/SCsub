#!/usr/bin/env python

Import("env")
Import("env_modules")

env_basisu = env_modules.Clone()

# Thirdparty source files

thirdparty_obj = []

# Not unbundled so far since not widespread as shared library
thirdparty_dir = "#thirdparty/basis_universal/"
# Sync list with upstream CMakeLists.txt
encoder_sources = [
    "basis_universal/encoder/apg_bmp.c",
    "basis_universal/encoder/basisu_astc_decomp.cpp",
    "basis_universal/encoder/basisu_backend.cpp",
    "basis_universal/encoder/basisu_basis_file.cpp",
    "basis_universal/encoder/basisu_bc7enc.cpp",
    "basis_universal/encoder/basisu_comp.cpp",
    "basis_universal/encoder/basisu_enc.cpp",
    "basis_universal/encoder/basisu_etc.cpp",
    "basis_universal/encoder/basisu_frontend.cpp",
    "basis_universal/encoder/basisu_global_selector_palette_helpers.cpp",
    "basis_universal/encoder/basisu_gpu_texture.cpp",
    "basis_universal/encoder/basisu_kernels_sse.cpp",
    "basis_universal/encoder/basisu_pvrtc1_4.cpp",
    "basis_universal/encoder/basisu_resampler.cpp",
    "basis_universal/encoder/basisu_resample_filters.cpp",
    "basis_universal/encoder/basisu_ssim.cpp",
    "basis_universal/encoder/basisu_uastc_enc.cpp",
    "basis_universal/encoder/jpgd.cpp",
    "basis_universal/encoder/lodepng.cpp",
    "basis_universal/transcoder/basisu_transcoder.cpp",
]
transcoder_sources = ["basis_universal/transcoder/basisu_transcoder.cpp"]
# Treat Basis headers as system headers to avoid raising warnings. Not supported on MSVC.
if not env.msvc:
    env_basisu.Append(CPPFLAGS=["-isystem", Dir(thirdparty_dir).path])
else:
    env_basisu.Prepend(CPPPATH=[thirdparty_dir])

if env["target"] == "debug":
    env_basisu.Append(CPPDEFINES=[("BASISU_DEVEL_MESSAGES", 1), ("BASISD_ENABLE_DEBUG_FLAGS", 1)])

env_thirdparty = env_basisu.Clone()
env_thirdparty.VariantDir("basis_universal", env.Dir(thirdparty_dir), duplicate=0)
env_thirdparty.disable_warnings()
if env["tools"]:
    env_thirdparty.Append(CPPDEFINES=["BASISU_NO_IMG_LOADERS"])
    env_thirdparty.add_source_files(thirdparty_obj, encoder_sources)
env_thirdparty.add_source_files(thirdparty_obj, transcoder_sources)
env.modules_sources += thirdparty_obj

# Godot source files

module_obj = []

env_basisu.add_source_files(module_obj, "*.cpp")
env.modules_sources += module_obj

# Needed to force rebuilding the module files when the thirdparty library is updated.
env.Depends(module_obj, thirdparty_obj)
