#!/usr/bin/env python

Import("env")
Import("env_modules")

env_gdnative = env_modules.Clone()
env_gdnative.add_source_files(env.modules_sources, "gdnative.cpp")
env_gdnative.add_source_files(env.modules_sources, "register_types.cpp")
env_gdnative.add_source_files(env.modules_sources, "android/*.cpp")
env_gdnative.add_source_files(env.modules_sources, "gdnative/*.cpp")
env_gdnative.add_source_files(env.modules_sources, "nativescript/*.cpp")
env_gdnative.add_source_files(env.modules_sources, "gdnative_library_singleton_editor.cpp")
env_gdnative.add_source_files(env.modules_sources, "gdnative_library_editor_plugin.cpp")


env_gdnative.Prepend(CPPPATH=[env_gdnative.Dir("include").srcnode(), env_gdnative.Dir("include")])

Export("env_gdnative")

SConscript("pluginscript/SCsub", duplicate=0)
SConscript("videodecoder/SCsub", duplicate=0)


from modules.gdnative import gdnative_builders

_, gensource = env_gdnative.Command(
    ["include/gdnative_api_struct.gen.h", "gdnative_api_struct.gen.cpp"],
    "gdnative_api.json",
    Action(gdnative_builders.build_gdnative_api_struct, "Generating GDNative API."),
)
env_modules.Append(CPPPATH=[env_gdnative.Dir("include")])

env_gdnative.add_source_files(env.modules_sources, [gensource])
